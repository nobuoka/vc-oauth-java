buildscript {
    ext {
        kotlin_version = '1.3.71'
        spek_version = '1.2.1'
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.71"
    id "org.jetbrains.dokka" version "0.10.1"
}

archivesBaseName = 'oauth-core'
description = "Kotlin implementation of OAuth 1.0 Protocol."

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

def javaApiUrl = 'http://docs.oracle.com/javase/8/docs/api/'
tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
    options.links(javaApiUrl)
}

// setup dependencies
dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testImplementation "org.jetbrains.spek:spek-api:$spek_version"
    testRuntimeOnly "org.jetbrains.spek:spek-junit-platform-engine:$spek_version"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher:1.6.2"

    constraints {
        testImplementation("org.jetbrains.kotlin:kotlin-reflect:$kotlin_version") {
            because 'Version of kotlin-reflect should be same as other Kotlin libs.'
        }
    }
}

test {
    useJUnitPlatform {
        includeEngines "spek"
    }
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}
javadoc.dependsOn(dokkaJavadoc)

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from "$buildDir/javadoc"
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId "${rootProject.ext.releaseProperties.artifactIdBase}-core"
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}
