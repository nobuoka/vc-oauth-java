buildscript {
    ext {
        kotlin_version = '1.2.21'
        spek_version = '1.2.1'
    }

    repositories {
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
        // TODO: Remove this repo once upgraded off eap to 0.9.16
        // See : https://github.com/vcschapp/tarro/commit/3e48a5361de697ece88e1e15dcbb13d16a8b3cb7
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap/' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.16-eap-3"
    }
}

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'org.junit.platform.gradle.plugin'

archivesBaseName = 'oauth-core'
description = "Kotlin implementation of OAuth 1.0 Protocol."

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

def javaApiUrl = 'http://docs.oracle.com/javase/8/docs/api/'
tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
    options.links(javaApiUrl)
}

// setup dependencies
dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testCompile "org.jetbrains.spek:spek-api:$spek_version"
    // spek-api depends on old version of kotlin-reflect, so an explicit dependency for newer version is provided
    testCompile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testRuntime "org.jetbrains.spek:spek-junit-platform-engine:$spek_version"
}

junitPlatform {
    filters {
        engines {
            include 'spek'
        }
    }
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}
javadoc.dependsOn(dokkaJavadoc)

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from "$buildDir/javadoc"
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId "${rootProject.ext.releaseProperties.artifactIdBase}-core"
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}
